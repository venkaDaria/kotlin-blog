/**
 * Overrides default build task,
 * can be used with --console=verbose to show called tasks
 */
build { finalizedBy(tasks['detekt']) }

/**
 * Detekt support
 */
detekt {
    config = files("$projectDir/src/main/resources/default-detekt-config.yml")
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
    reports {
        xml {
            enabled = false
        }
        html {
            enabled = true
            destination = file("$buildDir/reports/detekt.html")
        }
    }
}

/**
 * JaCoCo Support
 *
 * @use ./gradlew test [jacocoTestReport]
 */
apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("$buildDir/reports")
    }
}

// must be called before JaCoCo tasks
test {
    jacoco {
        append = false
    }

    // calls jacocoTestReport
    finalizedBy jacocoTestReport
}

/**
 * Sets Gradle version
 *
 * @use ./gradlew wrapper
 */
wrapper {
    gradleVersion = '4.10.3'
}