/**
 * Overrides default build task,
 * can be used with --console=verbose to show called tasks
 */
build { finalizedBy(tasks['detektCheck']) }

/**
 * Detekt support
 *
 * @use ./gradlew detektCheck
 */
detekt {
    version = "1.0.0.RC7-2"

    defaultProfile {
        config = "$projectDir/src/main/resources/default-detekt-config.yml"
        input = "$projectDir/src/"
        filters = [".*/resources/.*"]
        output = file("$buildDir/reports")
        outputName = "detekt"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

/**
 * JaCoCo Support
 *
 * @use ./gradlew test [jacocoTestReport]
 */
apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false

        html.destination file("$buildDir/reports")
    }
}

// must be called before JaCoCo tasks
test {
    // add TestNG support
    useTestNG()

    jacoco {
        append = false
    }

    // calls Cucumber tests
    finalizedBy cucumberTest

    // calls jacocoTestReport
    finalizedBy jacocoTestReport
}

/**
 * Sets Gradle version
 *
 * @use ./gradlew wrapper
 */
task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}